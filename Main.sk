
# This script was coded by TheNegativePanda

local function SelectGroups(p: players = all players):
    clear {SquadLife::Data::*}
    loop ceil(size of {_p::*} / 4) times:
        clear {_rp::*}
        loop 4 times:
            set {_rp::%loop-iteration-2%} to uuid of (a random element out of {_p::*})
            remove (player from {_rp::%loop-iteration-2%}) from {_p::*}
        loop {_rp::*}:
            set {SquadLife::Data::%loop-value-2%::Soulbounds::*} to {_rp::*} where [input != loop-value-2]

local function SplitGroup(id: uuids):
    loop {_id::*}:
        clear {SquadLife::Data::%loop-value%::Soulbounds::*}
    set {_size} to size of {_id::*} 
    loop 2 times:
        clear {_rp::*}
        loop ceil({_size} / 2) times:
            set {_rp::%loop-iteration-2%} to (a random element out of {_id::*} where [{_rp::*} does not contain input])
            remove {_rp::%loop-iteration-2%} from {_id::*} 
        loop {_rp::*}:
            set {SquadLife::Data::%loop-value-2%::Soulbounds::*} to {_rp::*} where [input != loop-value-2]
            
            
function UpdateTeam(p: player):
    set {_color::*} to light red, yellow, lime and green
    set team color of team of {_p} to {_color::%{SquadLife::Data::%{_p}'s uuid%::Lives}%}
    if {SquadLife::Data::%{_p}'s uuid%::Lives} <= 0:
        set team color of team of {_p} to gray
    else if {SquadLife::Data::%{_p}'s uuid%::Lives} > 4:
        set team color of team of {_p} to green

local function Start(players: players = all players):
    loop 3 times:
        send title "&a&l%4 - loop-iteration%" to {_players::*} 
        play sound "ui.button.click" to {_players::*}
        wait 2 seconds
    send title "&a&lYour Soulmates are..." to {_players::*} 
    play sound "entity.breeze.inhale" to {_players::*}
    SelectGroups({_players::*})
    loop {_players::*}:
        if team of loop-value isn't set:
            add loop-value to team named "%loop-value%"
        set {SquadLife::Data::%loop-value's uuid%::Lives} to 5
        UpdateTeam(loop-value)
    wait 1 second
    send title "&a&l???" to {_players::*}
    play sound "block.tinted_leaves using lime_block.bell" to {_players::*}
    damage {_players::*} by 0.1
    set {SquadLife::Session} to true


on join:
    if team of player is not set:
        add player to team named "%player%"
        UpdateTeam(player)
    player is alive
    loop players from {SquadLife::Data::%player's uuid%::Soulbounds::*}:
        if all: 
            loop-value is online
            loop-value is alive
        then:
            set health of player to health of loop-value

on respawn:
    loop players from {SquadLife::Data::%player's uuid%::Soulbounds::*}:
        if loop-value is alive:
            set health of player to health of loop-value





on damage of player:
    wait 1 tick
    if victim's metadata "resurrected" is true:
        show resurrection by totem at players from {SquadLife::Data::%victim's uuid%::Soulbounds::*}
        stop
    loop players from {SquadLife::Data::%victim's uuid%::Soulbounds::*}:
        if all:
            victim isn't blocking
            damage cause isn't unknown
            loop-value is online
        then:
            damage loop-value by final damage
            if loop-value is alive:
                set loop-value's health to victim's health  
            if victim isn't alive:
                damage loop-value by health of loop-value
on heal:
    wait 1 tick
    loop players from {SquadLife::Data::%player's uuid%::Soulbounds::*}:
        if all:
            heal reason isn't unknown
            loop-value is online
            loop-value is alive
        then:
            set health of loop-value to health of player



on death of player:
    {SquadLife::Data::%victim's uuid%::Lives} > 0
    set victim's metadata "hasDied" to true
    if {SquadLife::Session} = true:
        remove 1 from {SquadLife::Data::%victim's uuid%::Lives}
        loop {SquadLife::Data::%victim's uuid%::Soulbounds::*}:
            player from loop-value isn't set
            remove 1 from {SquadLife::Data::%loop-value%::Lives}
            UpdateTeam(offline player from loop-value)
            if {SquadLife::Data::%loop-value's uuid%::Lives} = 0:
                broadcast "&c%offline player from loop-value% has been eliminated!"
                set game mode of offline player from loop-value to spectator
        if {SquadLife::Data::%victim's uuid%::Lives} = 0:
            cancel event
            Eliminate(victim, "&c%uncolored death message%")
            stop
        UpdateTeam(victim)
        if all:
            damage cause isn't custom
            {SquadLife::Data::%victim's uuid%::Lives} = 3 or 1
        then:
            if any: 
                {SquadLife::Roles::Boogeyman::*} contains uuid of victim
                {SquadLife::Roles::Boogeyman::*} contains any of {SquadLife::Data::%uuid of victim%::Soulbounds::*}
            then:
                set {_i} to first index of (any of (victim's uuid and {SquadLife::Data::%{_p}'s uuid%::Soulbounds::*})) in {SquadLife::Roles::Boogeyman::*}
                delete {SquadLife::Roles::Boogeyman::%{_i}%}
                send "&aYou are no longer the boogeyman." to victim and players from {SquadLife::Data::%victim's uuid%::Soulbounds::*}
            wait 2 tick
            SplitGroup({SquadLife::Data::%victim's uuid%::Soulbounds::*} and uuid of victim)
    wait 2 ticks
    delete victim's metadata "hasDied"

local function Eliminate(p: player, deathMessage: string):
    strike lightning effect at {_p}
    set {Elim::%{_p}%} to true
    strike lightning effect at {_p}
    make 300 of lava at {_p} with offset vector(1,3,1) with extra 1 with force
    make 3000 of trial_omen at {_p} with offset vector(0, 100, 0) with extra 1 with force
    loop 5 times:
        if {SquadLife::Data::%{_p}'s uuid%::Lives} >= 1:
            stop
        make 300 of lava at {_p} with offset vector(1,3,1) with extra 1 with force
        wait 20 ticks
        show resurrection by totem on {_p} if loop-iteration = 4
    make 3000 of totem_of_undying at {_p} with offset vector(0, 3, 0) with extra 1 with force
    strike lightning effect at {_p} 
    play sound "entity.allay.ambient_without_item" with volume 0.75 at pitch 0.1 to all players 
    play sound "item.mace.smash_ground_heavy" with volume 0.75 at pitch 0.1 to all players
    delete {Elim::%{_p}%}
    set team option death_message_visibility of team of {_p} to never
    kill {_p}
    broadcast {_deathMessage}
    drop items in inventory of {_p} at {_p} if gamerule keep_inventory of world of {_p} is true
    send title "&c%{_p}%" with subtitle "&7has been eliminated!" to all players
    send title "&cYou have run out of lives!" with subtitle " " to {_p} 
    set game mode of {_p} to spectator
    set team color of team of {_p} to gray
    wait 1 tick
    set team option death_message_visibility of team of {_p} to always
    send "&4Better luck next time." to {_p}

on resurrect attempt:
    set {_players::*} to players from {SquadLife::Data::%player's uuid%::Soulbounds::*} where [input's metadata "hasDied" is set]
    if any: 
        {SquadLife::Data::%player's uuid%::Lives} <= 0
        size of {_players::*} >= 1
    then:
        cancel event
        stop
    if any:
        player's offhand item = totem of undying
        player's held item = totem of undying
    then:
        set player's metadata "resurrected" to true
        wait 2 ticks
        delete player's metadata "resurrected"
        



on player move:
    if {Elim::%player%} is true:
        cancel event
            
on damage:
    if any: 
        {Elim::%attacker%} is true
        {Elim::%victim%} is true
    then:
        cancel event

on tab complete of "/squadlife" or "/life:squadlife":
    set tab completions for position 1 to "modify", "boogeyman", "session" and "reset"
    if tab arg-1 = "modify":
        set tab completions for position 2 to "lives", "soulbounds" and "eliminate"
        set tab completions for position 3 to "@a", "@s", "@r" and "@p" if tab arg-2 != "soulbounds"
        add all players to tab completions for position 3
        if tab arg-2 = "lives":
            set tab completions for position 4 to "set", "remove" and "add"
    else if tab arg-1 = "boogeyman":
        set tab completions for position 2 to "roll", "cure", "curse" and "fail"
        set tab completions for position 3 to "@a", "@s", "@r", "@p" and all players if tab arg-2 = "cure" or "curse"
    set tab completions for position 2 to "start", "pause" and "end" if tab arg-1 = "session"
    

        



command /squadlife [<text>] [<text>] [<text>] [<text>] [<text>]:
    description: description
    permission: permission
    prefix: life
    trigger:
        if arg-1 = "modify":
            set {_p} to arg-3 parsed as player
            
            if arg-2 = "lives":
                set {_lives} to arg-5 parsed as integer
                {_lives} is set
                if arg-4 != "set", "remove" or "add":
                    send "&c%arg-4% is not a valid argument." 
                
                switch arg-3:
                    case "@a":
                        loop all players:
                            ModifyLives(loop-player, arg-4, {_lives})
                        send "&aSet lives of all players to %{_lives}%" if arg-4 = "set"
                        send "&aRemoved %{_lives}% lives from all players" if arg-4 = "remove"
                        send "&aAdded %{_lives}% lives to all players" if arg-4 = "add"
                    case "@s":
                        set {_p} to player
                    case "@r":
                        set {_p} to a random player out of all players
                    case "@p":
                        set {_p} to nearest player around player excluding player
                {_p} is set
                ModifyLives({_p}, arg-4, {_lives})
                send "&a%{_p}%'s lives have been modified. They now have %{SquadLife::Data::%{_p}'s uuid%::Lives}% Lives."
                UpdateTeam({_p})
            else if arg-2 = "soulbounds":
                {_p} is set
                EditGroups({_p}, player)
            else if arg-2 = "eliminate":
                {_p} is set
                Eliminate({_p}, "&c%{_p}% was eliminated by %sender% using commands")
        else if arg-1 = "session":
            if arg-2 = "start":
                broadcast "&a&lThe Session has started!"
                play sound "block.beacon.activate" to all players
                set {SquadLife::Session} to true
            else if arg-2 = "end":
                broadcast "&c&lThe Session is over"
                play sound "block.beacon.deactivate" to all players
                set {SquadLife::Session} to false
            else if arg-2 = "pause":
                if {SquadLife::Session} = "paused" or false:
                    broadcast "&a&lSession has been unpaused"
                    set {SquadLife::Session} to true
                    play sound "block.beacon.activate" to all players
                else if {SquadLife::Session} = true:
                    broadcast "&c&lSession has been paused"
                    send "&aRun this command again the unpause it."
                    set {SquadLife::Session} to "paused"
                    play sound "block.beacon.deactivate" to all players
        else if arg-1 = "reset":
            if arg-2 = "confirm":
                clear {SquadLife::*}
                broadcast "&c&lThis Season's save data has been cleared by %sender%"
                wait 5 seconds
                set {_b} to mini message from "<green><click:run_command:'/life:squadlife start'>Click here to start a new season."
                if sender is a player:
                    send component {_b}
                else:	
                    send component {_b} to all operators
            else:
                send formatted "<red>You are about to delete all save data for this season."
                wait 1 second
                send component mini message from "<red>This action is <b>PERMANENT</b>!"
                wait 1 second
                send formatted "<red>Run <underline>/%command% reset confirm.<reset><red> To complete the reset." 
        else if arg-1 = "boogeyman": 
            if arg-2 = "roll":
                broadcast "&cThe boogeymen will be selected soon."
                play sound "entity.lightning_bolt.thunder" to all players
                wait 15 seconds
                set {_players::*} to all players where [{SquadLife::Data::%uuid of input%::Lives} >= 2] except all players where [game mode of input != survival]
                set {_i} to 1
                set {_i} to arg-3 parsed as integer if arg-3 parsed as integer is set
                RollBoogeyman({_players::*}, {_i})
            else if arg-2 = "fail":
                FailBoogeymen()
            else if arg-2 = "cure" or "curse":
                switch arg-3:
                    case "@r":
                        set {_p} to a random player out of all players
                    case "@p":
                        set {_p} to nearest player around player excluding player
                    case "@s":
                        set {_p} to player
                    case "@a":
                        send "&cEvery player is now a Boogeyman."
                        loop all players:
                            set {_p} to loop-player
                            switch arg-2:
                                case "cure":
                                    CureBoogeyman({_p})
                                case "curse":
                                    add uuid of {_p} to {SquadLife::Roles::Boogeyman::*}
                    default:
                        set {_p} to arg-3 parsed as player
                if {_p} is set:
                    switch arg-2:
                        case "cure":
                            CureBoogeyman({_p})
                        case "curse":
                            add uuid of {_p} to {SquadLife::Roles::Boogeyman::*}
                            send "&7You are &c&lThe Boogeyman&r&7. You or one of your soulbounds must kill another player before time runs out. If you &cfail &7everyone in your squad will be set down to their last life." to {_p}
                            play sound "block.note_block.didgeridoo" to {_p}
                            send "&a%{_p}% has been added as a boogeyman."
        else if arg-1 = "start":
            send "&a%sender% has started a new season." to all operators
            Start(all players where [game mode of input != spectator])

                    

local function ModifyLives(p: player, action: text, lives: integer):
    if {_action} = "set":
        set {SquadLife::Data::%{_p}'s uuid%::Lives} to {_lives}
    else if {_action} = "remove":
        remove {_lives} from {SquadLife::Data::%{_p}'s uuid%::Lives}
    else if {_action} = "add":
        add {_lives} to {SquadLife::Data::%{_p}'s uuid%::Lives}
    UpdateTeam({_p})           
                        



    