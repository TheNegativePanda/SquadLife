
local function SelectGroups(p: players = all players):
    clear {SquadLife::Data::*}
    loop ceil(size of {_p::*} / 4) times:
        clear {_rp::*}
        loop 4 times:
            set {_rp::%loop-iteration-2%} to uuid of (a random element out of {_p::*})
            remove (player from {_rp::%loop-iteration-2%}) from {_p::*}
        loop {_rp::*}:
            set {SquadLife::Data::%loop-value-2%::Soulbounds::*} to {_rp::*} where [input != loop-value-2]

local function SplitGroup(id: uuids):
    loop {_id::*}:
        clear {SquadLife::Data::%loop-value%::Soulbounds::*}
    set {_size} to size of {_id::*} 
    loop 2 times:
        clear {_rp::*}
        loop ceil({_size} / 2) times:
            set {_rp::%loop-iteration-2%} to (a random element out of {_id::*} where [{_rp::*} does not contain input])
            remove {_rp::%loop-iteration-2%} from {_id::*} 
        loop {_rp::*}:
            set {SquadLife::Data::%loop-value-2%::Soulbounds::*} to {_rp::*} where [input != loop-value-2]
            
            
local function UpdateTeam(p: player):
    set {_color::*} to light red, yellow, lime and green
    set team color of team of {_p} to {_color::%{SquadLife::Data::%{_p}'s uuid%::Lives}%}
    if {SquadLife::Data::%{_p}'s uuid%::Lives} <= 0:
        set team color of team of {_p} to gray
    else if {SquadLife::Data::%{_p}'s uuid%::Lives} > 4:
        set team color of team of {_p} to green

local function Start(players: players = all players):
    loop 3 times:
        send title "&a&l%4 - loop-iteration%" to {_players::*} 
        play sound "ui.button.click" to {_players::*}
        wait 2 seconds
    send title "&a&lYour Soulmates are..." to {_players::*} 
    play sound "entity.breeze.inhale" to {_players::*}
    SelectGroups({_players::*})
    loop {_players::*}:
        if team of loop-value isn't set:
            add loop-value to team named "%loop-value%"
        set {SquadLife::Data::%loop-value's uuid%::Lives} to 5
        UpdateTeam(loop-value)
    wait 1 second
    send title "&a&l???" to {_players::*}
    play sound "block.tinted_leaves using lime_block.bell" to {_players::*}
    damage {_players::*} by 0.1
    # loop {_players::*}: 
    #     send title "&a%players from {SquadLife::Data::%uuid of loop-value%::Soulbounds::*}%" to loop-value
    #     play sound "block.tinted_leaves using lime_block.bell" to loop-value


on join:
    if team of player is not set:
        add player to team named "%player%"
        UpdateTeam(player)
    player is alive
    loop players from {SquadLife::Data::%player's uuid%::Soulbounds::*}:
        if all: 
            loop-value is online
            loop-value is alive
        then:
            set health of player to health of loop-value

on respawn:
    loop players from {SquadLife::Data::%player's uuid%::Soulbounds::*}:
        if loop-value is alive:
            set health of player to health of loop-value



on damage of player:
    wait 1 tick
    loop players from {SquadLife::Data::%victim's uuid%::Soulbounds::*}:
        if all:
            victim isn't blocking
            damage cause isn't unknown
            loop-value is online
        then:
            damage loop-value by final damage
            if loop-value is alive:
                set loop-value's health to victim's health  
            if victim isn't alive:
                damage loop-value by health of loop-value
on heal:
    wait 1 tick
    loop players from {SquadLife::Data::%player's uuid%::Soulbounds::*}:
        if all:
            heal reason isn't unknown
            loop-value is online
            loop-value is alive
        then:
            set health of loop-value to health of player

on death of player:
    if all: 
        {SquadLife::Data::%victim's uuid%::Lives} <= 0
        {-dm::%victim%} is set
    then:
        set death message to "&c%{-dm::%victim%}%"
        delete {-dm::%victim%}
    {SquadLife::Data::%victim's uuid%::Lives} > 0
    if {SquadLife::Session} = true:
        remove 1 from {SquadLife::Data::%victim's uuid%::Lives}
        loop players from {SquadLife::Data::%victim's uuid%::Soulbounds::*}:
            if loop-value is not online:
                remove 1 from {SquadLife::Data::%loop-value's uuid%::Lives}
                UpdateTeam(loop-value)
                if {SquadLife::Data::%loop-value's uuid%::Lives} = 0:
                    broadcast "&c%loop-value% has been eliminated!"
                    set game mode of loop-value to spectator
        if {SquadLife::Data::%victim's uuid%::Lives} = 0:
            cancel event
            Eliminate(victim)
            set {-dm::%victim%} to uncolored death message
            stop
        UpdateTeam(victim)
        if all:
            damage cause isn't custom
            {SquadLife::Data::%victim's uuid%::Lives} = 3 or 1
        then:
            if any: 
                {SquadLife::Roles::Boogeyman::*} contains uuid of victim
                {SquadLife::Roles::Boogeyman::*} contains any of {SquadLife::Data::%uuid of victim%::Soulbounds::*}
            then:
                remove ({SquadLife::Data::%victim's uuid%::Soulbounds::*} and uuid of victim) from {SquadLife::Roles::Boogeyman::*} 
                send "&aYou are no longer the boogeyman." to victim and players from {SquadLife::Data::%victim's uuid%::Soulbounds::*}
            wait 2 tick
            SplitGroup({SquadLife::Data::%victim's uuid%::Soulbounds::*} and uuid of victim)
            

local function Eliminate(p: player, msg: string = "none"):
    make 100 of dust using dustOption(red, 1.5) at {_p} with offset vector(1,1,1) with force
    set {_loc} to location of {_p}
    set {_d} to 400 - altitude of {_loc} 
    set {Elim::%{_p}%} to true
    loop ({_d} / 2) times:
        set {_offsetY} to ({_d} - loop-iteration * 2)
        set {_dx} to 3 - {_d} / {_offsetY} 
        if {_dx} < 0.5:
            set {_dx} to 0.5
        else if {_offsetY} <= 0:
            set {_dx} to 0.5
        
        make 140 of dust using dustOption(light purple, 3) at ({_loc} ~ vector(({_offsetY} * 0.2) * cos(loop-iteration - 1), {_offsetY}, ({_offsetY} * 0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with force
        make 140 of dust using dustOption(light blue, 3) at ({_loc} ~ vector({_offsetY} * (-0.2) * sin(loop-iteration -1), {_offsetY}, {_offsetY} * 0.2 * cos(loop-iteration - 1))) with offset vector(1,1,1) with force
        make 140 of dust using dustOption(lime, 3) at ({_loc} ~ vector({_offsetY} * 0.2 * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * cos(loop-iteration - 1))) with offset vector(1,1,1) with force
        make 140 of dust using dustOption(yellow, 3) at ({_loc} ~ vector({_offsetY} * (-0.2) * cos(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with force
        make 140 of dust using dustOption(red, 3) at ({_loc} ~ vector(0, {_offsetY}, 0)) with offset vector({_dx},1,{_dx}) with force
        # make 70 glow at ({_loc} ~ vector(({_offsetY} * 0.2) * cos(loop-iteration - 1), {_offsetY}, ({_offsetY} * 0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with force
        # make 70 glow at ({_loc} ~ vector({_offsetY} * (-0.2) * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * 0.2 * cos(loop-iteration - 1))) with offset vector(1,1,1) with force
        # make 70 glow at ({_loc} ~ vector({_offsetY} * 0.2 * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * cos(loop-iteration - 1))) with offset vector(1,1,1) with force
        # make 70 glow at ({_loc} ~ vector({_offsetY} * (-0.2) * cos(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with force
        # make 70 glow at ({_loc} ~ vector(0, {_offsetY}, 0)) with offset vector(1,1,1) with force
        # make 70 explosion at ({_loc} ~ vector(({_offsetY} * 0.2) * cos(loop-iteration - 1), {_offsetY}, ({_offsetY} * 0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with force
        # make 70 explosion at ({_loc} ~ vector({_offsetY} * (-0.2) * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * 0.2 * cos(loop-iteration - 1))) with offset vector(1,1,1) with force
        # make 70 explosion at ({_loc} ~ vector({_offsetY} * 0.2 * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * cos(loop-iteration - 1))) with offset vector(1,1,1) with force
        # make 70 explosion at ({_loc} ~ vector({_offsetY} * (-0.2) * cos(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with force
        # make 70 explosion at ({_loc} ~ vector(0, {_offsetY}, 0)) with offset vector(1,1,1) with force
        
        wait 1 tick
    make 100 of flash at {_p} with offset vector(1,1,1) with force
    # loop blocks in radius 5 around {_p}:
    #     loop-block != air
    #     chance of 70%:
    #         set loop-block to sculk
    #     else:
    #         set loop-block to soul sand
    strike lightning effect at {_p}
    drop items in inventory of {_p} at {_p}
    play sound "entity.allay.death" to all players
    send title "&c&l%{_p}%" with subtitle "&f&lhas been eliminated!" to all players where [input != {_p}]
    send title "&c&lYou have ran out of lives!" to {_p}
    broadcast {_msg} if {_msg} != "none"
    delete {Elim::%{_p}%}
    kill {_p}
    set game mode of {_p} to spectator

on player move:
    if {Elim::%player%} is true:
        cancel event
            
on damage:
    if any: 
        {Elim::%attacker%} is true
        {Elim::%victim%} is true
    then:
        cancel event

command /animation:
    description: description
    permission: permission
    trigger:
        set {_p} to player
        set {_loc} to location of {_p}
        set {_d} to 400 - altitude of {_loc} 
        loop ({_d} / 2) times:
            set {_offsetY} to ({_d} - loop-iteration * 2)
            set {_dx} to 3 - {_d} / {_offsetY} 
            if {_dx} < 0.5:
                set {_dx} to 0.5
            else if {_offsetY} <= 0:
                set {_dx} to 0.5
            
            # make 140 of dust using dustOption(light purple, 3) at ({_loc} ~ vector(({_offsetY} * 0.2) * cos(loop-iteration - 1), {_offsetY}, ({_offsetY} * 0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with force
            # make 140 of dust using dustOption(light blue, 3) at ({_loc} ~ vector({_offsetY} * (-0.2) * sin(loop-iteration -1), {_offsetY}, {_offsetY} * 0.2 * cos(loop-iteration - 1))) with offset vector(1,1,1) with force
            # make 140 of dust using dustOption(lime, 3) at ({_loc} ~ vector({_offsetY} * 0.2 * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * cos(loop-iteration - 1))) with offset vector(1,1,1) with force
            # make 140 of dust using dustOption(yellow, 3) at ({_loc} ~ vector({_offsetY} * (-0.2) * cos(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with force
            # make 140 of dust using dustOption(red, 3) at ({_loc} ~ vector(0, {_offsetY}, 0)) with offset vector({_dx},1,{_dx}) with force
            # make 140 glow at ({_loc} ~ vector(({_offsetY} * 0.2) * cos(loop-iteration - 1), {_offsetY}, ({_offsetY} * 0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with force
            # make 140 glow at ({_loc} ~ vector({_offsetY} * (-0.2) * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * 0.2 * cos(loop-iteration - 1))) with offset vector(1,1,1) with force
            # make 140 glow at ({_loc} ~ vector({_offsetY} * 0.2 * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * cos(loop-iteration - 1))) with offset vector(1,1,1) with force
            # make 140 glow at ({_loc} ~ vector({_offsetY} * (-0.2) * cos(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with force
            # make 140 glow at ({_loc} ~ vector(0, {_offsetY}, 0)) with offset vector(1,1,1) with force
            make 140 tinted_leaves using light purple at ({_loc} ~ vector(({_offsetY} * 0.2) * cos(loop-iteration - 1), {_offsetY}, ({_offsetY} * 0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with extra 0.1 with force
            make 140 tinted_leaves using light blue at ({_loc} ~ vector({_offsetY} * (-0.2) * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * 0.2 * cos(loop-iteration - 1))) with offset vector(1,1,1) with extra 0.1 with force
            make 140 tinted_leaves using lime at ({_loc} ~ vector({_offsetY} * 0.2 * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * cos(loop-iteration - 1))) with offset vector(1,1,1) with extra 0.1 with force
            make 140 tinted_leaves using yellow at ({_loc} ~ vector({_offsetY} * (-0.2) * cos(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with extra 0.1 with force
            make 140 tinted_leaves using red at ({_loc} ~ vector(0, {_offsetY}, 0)) with offset vector(1,1,1) with extra 0.1 with force
            
            wait 1 tick


on tab complete of "/squadlife":
    set tab completions for position 1 to "modify", "boogeyman", "session" and "reset"
    if tab arg-1 = "modify":
        set tab completions for position 2 to "lives", "soulbounds" and "eliminate"
        set tab completions for position 3 to "@a", "@s", "@r", "@p" if tab arg-2 != "soulbounds"
        add all players to tab completions for position 3
        if tab arg-2 = "lives":
            set tab completions for position 4 to "set", "remove" and "add"
    else if tab arg-1 = "boogeyman":
        set tab completions for position 2 to "roll", "cure", "curse" and "fail"
        set tab completions for position 3 to "@a", "@s", "@r", "@p" and all players where [{SquadLife::Data::%uuid of input%::Lives} > 0] if tab arg-2 = "cure" or "curse"
    set tab completions for position 2 to "start", "pause" and "end" if tab arg-1 = "session"
    

        



command /squadlife [<text>] [<text>] [<text>] [<text>] [<text>]:
    description: description
    permission: permission
    prefix: life
    trigger:
        if arg-1 = "modify":
            set {_p} to arg-3 parsed as player
        
            if arg-2 = "lives":
                set {_lives} to arg-5 parsed as integer
                {_lives} is set
                if arg-4 != "set", "remove" or "add":
                    send "&c%arg-4% is not a valid argument." 
                else if {_p} is set:
                    ModifyLives({_p}, arg-4, {_lives})
                    send "&a%{_p}%'s lives have been modified. They now have %{SquadLife::Data::%{_p}'s uuid%::Lives}% Lives."
                    UpdateTeam({_p})
                else if arg-3 contains "@": 
                    if arg-3 = "@a":    
                        loop all players:
                            ModifyLives(loop-player, arg-4, {_lives})
                        send "&aSet lives of all players to %{_lives}%" if arg-4 = "set"
                        send "&aRemoved %{_lives}% lives from all players" if arg-4 = "remove"
                        send "&aAdded %{_lives}% lives to all players" if arg-4 = "add"
                    else if arg-3 = "@r":
                        set {_p} to a random player out of all players
                        ModifyLives({_p}, arg-4, {_lives})
                        send "&a%{_p}%'s lives have been modified. They now have %{SquadLife::Data::%{_p}'s uuid%::Lives}% Lives."
                    else if arg-3 = "@s":
                        set {_p} to sender
                        ModifyLives({_p}, arg-4, {_lives})
                        send "&a%{_p}%'s lives have been modified. They now have %{SquadLife::Data::%{_p}'s uuid%::Lives}% Lives."
                    else:
                        set {_p} to nearest player around player excluding player
                        ModifyLives({_p}, arg-4, {_lives})
                        send "&a%{_p}%'s lives have been modified. They now have %{SquadLife::Data::%{_p}'s uuid%::Lives}% Lives."
            else if arg-2 = "soulbounds":
                EditGroups({_p}, player)
        else if arg-1 = "session":
            if arg-2 = "start":
                broadcast "&a&lThe Session has started!"
                play sound "block.beacon.activate" to all players
                set {SquadLife::Session} to true
            else if arg-2 = "end":
                broadcast "&c&lThe Session is over"
                play sound "block.beacon.deactivate" to all players
                set {SquadLife::Session} to false
            else if arg-2 = "pause":
                if {SquadLife::Session} = "paused" or false:
                    broadcast "&a&lSession has been unpaused"
                    set {SquadLife::Session} to true
                    play sound "block.beacon.activate" to all players
                else if {SquadLife::Session} = true:
                    broadcast "&a&lSession has been paused"
                    send "&aRun this command again the unpause it."
                    set {SquadLife::Session} to "paused"
                    play sound "block.beacon.deactivate" to all players
        else if arg-1 = "reset":
            if arg-2 = "confirm":
                clear {SquadLife::*}
                broadcast "&c&lThis Season's save data has been cleared by %sender%"
                wait 5 seconds
                set {_b} to mini message from "<green><click:run_command:'/life:squadlife start'>Click here to start a new season."
                if sender is a player:
                    send component {_b}
                else:	
                    send component {_b} to all operators
            else:
                send formatted "<red>You are about to delete all save data for this season."
                wait 1 second
                send component mini message from "<red>This action is <b>PERMANENT</b>!"
                wait 1 second
                send formatted "<red>Run <underline>/%command% reset confirm.<reset><red> To complete the reset." 
        else if arg-1 = "boogeyman": 
            if arg-2 = "roll":
                broadcast "&cThe boogeymen will be selected soon."
                play sound "entity.lightning_bolt.thunder" to all players
                wait 15 seconds
                set {_players::*} to all players where [{SquadLife::Data::%uuid of input%::Lives} >= 2] except all players where [game mode of input != survival]
                set {_i} to 1
                set {_i} to arg-3 parsed as integer if arg-3 parsed as integer is set
                RollBoogeyman({_players::*}, {_i})
            else if arg-2 = "fail":
                FailBoogeymen()
            

local function ModifyLives(p: player, action: text, lives: integer):
    if {_action} = "set":
        set {SquadLife::Data::%{_p}'s uuid%::Lives} to {_lives}
    else if {_action} = "remove":
        remove {_lives} from {SquadLife::Data::%{_p}'s uuid%::Lives}
    else if {_action} = "add":
        add {_lives} to {SquadLife::Data::%{_p}'s uuid%::Lives}
    UpdateTeam({_p})           
                        



    