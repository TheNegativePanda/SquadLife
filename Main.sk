command /setlives <int>:
    description: description
    permission: permission
    trigger:
        set {SquadLife::Data::%player's uuid%::Lives} to arg-1
        UpdateTeam(player)

local function SelectGroups(p: players = all players):
    clear {SquadLife::Data::*}
    loop ceil(size of {_p::*} / 4) times:
        clear {_rp::*}
        loop 4 times:
            set {_rp::%loop-iteration-2%} to uuid of (a random element out of {_p::*})
            remove (player from {_rp::%loop-iteration-2%}) from {_p::*}
        loop {_rp::*}:
            set {SquadLife::Data::%loop-value-2%::Soulbounds::*} to {_rp::*} where [input != loop-value-2]

local function SplitGroup(id: uuids):
    loop {_id::*}:
        clear {SquadLife::Data::%loop-value%::Soulbounds::*}
    set {_size} to size of {_id::*} 
    loop 2 times:
        clear {_rp::*}
        loop ceil({_size} / 2) times:
            set {_rp::%loop-iteration-2%} to (a random element out of {_id::*} where [{_rp::*} does not contain input])
            remove {_rp::%loop-iteration-2%} from {_id::*} 
        loop {_rp::*}:
            set {SquadLife::Data::%loop-value-2%::Soulbounds::*} to {_rp::*} where [input != loop-value-2]
            
            
local function UpdateTeam(p: player):
    set {_color::*} to light red, yellow, lime, light blue, light purple
    set team color of team of {_p} to {_color::%{SquadLife::Data::%{_p}'s uuid%::Lives}%}
    if {SquadLife::Data::%{_p}'s uuid%::Lives} <= 0:
        set team color of team of {_p} to gray
    else if {SquadLife::Data::%{_p}'s uuid%::Lives} > 5:
        set team color of team of {_p} to light purple

local function Start(players: players = all players):
    loop 3 times:
        send title "&a&l%4 - loop-iteration%" to {_players::*} 
        play sound "ui.button.click" to {_players::*}
        wait 2 seconds
    send title "&a&lYour Soulmates are..." to {_players::*} 
    play sound "entity.breeze.inhale" to {_players::*}
    SelectGroups({_players::*})
    loop {_players::*}:
        if team of loop-value isn't set:
            add loop-value to team named "%loop-value%"
        set {SquadLife::Data::%loop-value's uuid%::Lives} to 5
        UpdateTeam(loop-value)
    wait 1 second
    send title "&a&l???" to {_players::*}
    play sound "block.note_block.bell" to {_players::*}
    damage {_players::*} by 0.1
    # loop {_players::*}: 
    #     send title "&a%players from {SquadLife::Data::%uuid of loop-value%::Soulbounds::*}%" to loop-value
    #     play sound "block.note_block.bell" to loop-value


on join:
    if team of player is not set:
        add player to team named "%player%"
        UpdateTeam(player)
    player is alive
    loop players from {SquadLife::Data::%player's uuid%::Soulbounds::*}:
        if all: 
            loop-value is online
            loop-value is alive
        then:
            set health of player to health of loop-value

on respawn:
    loop players from {SquadLife::Data::%player's uuid%::Soulbounds::*}:
        if loop-value is alive:
            set health of player to health of loop-value



on damage of player:
    wait 1 tick
    loop players from {SquadLife::Data::%victim's uuid%::Soulbounds::*}:
        if all:
            victim isn't blocking
            damage cause isn't unknown
            loop-value is online
        then:
            damage loop-value by final damage
            if loop-value is alive:
                set loop-value's health to victim's health  
            if victim isn't alive:
                damage loop-value by health of loop-value
on heal:
    wait 1 tick
    loop players from {SquadLife::Data::%player's uuid%::Soulbounds::*}:
        if all:
            heal reason isn't unknown
            loop-value is online
            loop-value is alive
        then:
            set health of loop-value to health of player

on death of player:
    if all: 
        {SquadLife::Data::%victim's uuid%::Lives} <= 0
        {-dm::%victim%} is set
    then:
        set death message to "&c%{-dm::%victim%}%"
        delete {-dm::%victim%}
    {SquadLife::Data::%victim's uuid%::Lives} > 0
    if {SquadLife::Session} != true:
        remove 1 from {SquadLife::Data::%victim's uuid%::Lives}
        loop players from {SquadLife::Data::%victim's uuid%::Soulbounds::*}:
            if loop-value is not online:
                remove 1 from {SquadLife::Data::%loop-value's uuid%::Lives}
                UpdateTeam(loop-value)
                if {SquadLife::Data::%loop-value's uuid%::Lives} = 0:
                    broadcast "&c%loop-value% has been eliminated!"
                    set game mode of loop-value to spectator
        UpdateTeam(victim)
        if {SquadLife::Data::%victim's uuid%::Lives} = 0:
            cancel event
            Eliminate(victim)
            set {-dm::%victim%} to uncolored death message
        if all:
            damage cause isn't custom
            {SquadLife::Data::%victim's uuid%::Lives} = 3 or 1
        then:
            wait 2 tick
            SplitGroup({SquadLife::Data::%victim's uuid%::Soulbounds::*} and uuid of victim)
       

local function Eliminate(p: player, msg: string = "none"):
    make 100 of dust using dustOption(red, 1.5) at {_p} with offset vector(1,1,1) with force
    set {_loc} to location of {_p}
    set {_d} to 400 - altitude of {_loc} 
    set {Elim::%{_p}%} to true
    loop ({_d} / 2) times:
        set {_offsetY} to ({_d} - loop-iteration * 2)
        set {_dx} to 3 - {_d} / {_offsetY} 
        if {_dx} < 0.5:
            set {_dx} to 0.5
        else if {_offsetY} <= 0:
            set {_dx} to 0.5
        
        # make 140 of dust using dustOption(light purple, 3) at ({_loc} ~ vector(({_offsetY} * 0.2) * cos(loop-iteration - 1), {_offsetY}, ({_offsetY} * 0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with force
        # make 140 of dust using dustOption(light blue, 3) at ({_loc} ~ vector({_offsetY} * (-0.2) * sin(loop-iteration -1), {_offsetY}, {_offsetY} * 0.2 * cos(loop-iteration - 1))) with offset vector(1,1,1) with force
        # make 140 of dust using dustOption(lime, 3) at ({_loc} ~ vector({_offsetY} * 0.2 * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * cos(loop-iteration - 1))) with offset vector(1,1,1) with force
        # make 140 of dust using dustOption(yellow, 3) at ({_loc} ~ vector({_offsetY} * (-0.2) * cos(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * sin(loop-iteration - 1))) with offset vector(1,1,1) with force
        # make 140 of dust using dustOption(red, 3) at ({_loc} ~ vector(0, {_offsetY}, 0)) with offset vector({_dx},1,{_dx}) with force
        make 70 glow at ({_loc} ~ vector(({_offsetY} * 0.2) * cos(loop-iteration - 1), {_offsetY}, ({_offsetY} * 0.2) * sin(loop-iteration - 1))) with offset vector(0.5,0.5,0.5) with force
        make 70 glow at ({_loc} ~ vector({_offsetY} * (-0.2) * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * 0.2 * cos(loop-iteration - 1))) with offset vector(0.5,0.5,0.5) with force
        make 70 glow at ({_loc} ~ vector({_offsetY} * 0.2 * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * cos(loop-iteration - 1))) with offset vector(0.5,0.5,0.5) with force
        make 70 glow at ({_loc} ~ vector({_offsetY} * (-0.2) * cos(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * sin(loop-iteration - 1))) with offset vector(0.5,0.5,0.5) with force
        make 70 glow at ({_loc} ~ vector(0, {_offsetY}, 0)) with offset vector(0.5,0.5,0.5) with force
        make 70 sculk_soul at ({_loc} ~ vector(({_offsetY} * 0.2) * cos(loop-iteration - 1), {_offsetY}, ({_offsetY} * 0.2) * sin(loop-iteration - 1))) with offset vector(0.5,0.5,0.5) with force
        make 70 sculk_soul at ({_loc} ~ vector({_offsetY} * (-0.2) * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * 0.2 * cos(loop-iteration - 1))) with offset vector(0.5,0.5,0.5) with force
        make 70 sculk_soul at ({_loc} ~ vector({_offsetY} * 0.2 * sin(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * cos(loop-iteration - 1))) with offset vector(0.5,0.5,0.5) with force
        make 70 sculk_soul at ({_loc} ~ vector({_offsetY} * (-0.2) * cos(loop-iteration - 1), {_offsetY}, {_offsetY} * (-0.2) * sin(loop-iteration - 1))) with offset vector(0.5,0.5,0.5) with force
        make 70 sculk_soul at ({_loc} ~ vector(0, {_offsetY}, 0)) with offset vector(0.5,0.5,0.5) with force
        
        wait 1 tick
    make 100 of flash at {_p} with offset vector(1,1,1) with force
    loop blocks in radius 5 around {_p}:
        loop-block != air
        chance of 70%:
            set loop-block to sculk
        else:
            set loop-block to soul sand
    strike lightning effect at {_p}
    drop items in inventory of {_p} at {_p}
    play sound "entity.allay.death" to all players
    send title "&c&l%{_p}%" with subtitle "&f&lhas been eliminated!" to all players where [input != {_p}]
    send title "&c&lYou have ran out of lives!" to {_p}
    broadcast {_msg} if {_msg} != "none"
    delete {Elim::%{_p}%}
    kill {_p}
    set game mode of {_p} to spectator

on player move:
    if {Elim::%player%} is true:
        cancel event
            
on damage:
    if any: 
        {Elim::%attacker%} is true
        {Elim::%victim%} is true
    then:
        cancel event

