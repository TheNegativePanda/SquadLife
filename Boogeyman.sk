# This script was coded by TheNegativePanda

function RollBoogeyman(p: players, amount: integer):
    set {_color::*} to lime, yellow and light red
    loop 3 times:
        send title formatted "<%{_color::%loop-iteration%}%>%4 - loop-iteration%" to {_p::*}
        play sound "ui.button.click" to {_p::*}
        wait 2 seconds
    send title "&e&lYou are..." to {_p::*}
    play sound "entity.breeze.inhale" to {_p::*}
    wait 2 second
    loop {_amount} times:
        set {_selected} to a random player out of {_p::*}
        set {SquadLife::Roles::Boogeyman::%loop-iteration%} to uuid of {_selected}
        loop {_selected} and ((players from {SquadLife::Data::%uuid of {_selected}%::Soulbounds::*})):
            send title "&c&lThe Boogeyman" to loop-value-2
            play sound "block.note_block.didgeridoo" to loop-value-2
            send "&7You are &c&lThe Boogeyman&r&7. You or one of your soulbounds must kill another player before time runs out. If you &cfail &7everyone in your squad will be set down to their last life." to loop-value-2
            remove loop-value-2 from {_p::*} 
    send title "&a&lNOT the Boogeyman" to {_p::*}
    play sound "block.note_block.bell" to {_p::*}
    
on death of player:
    {SquadLife::Session} = true
    wait 1 tick
    {SquadLife::Data::%uuid of victim%::Lives} >= 1
    if all: 
        attacker isn't a player
        damage cause isn't custom
    then:
        set metadata value "Claimable" of victim to true 

    if any: 
        {SquadLife::Roles::Boogeyman::*} contains uuid of attacker
        {SquadLife::Roles::Boogeyman::*} contains any of {SquadLife::Data::%uuid of attacker%::Soulbounds::*}
    then:
        CureBoogeyman(attacker)
        send "&a%attacker% has killed %victim%. You are cured" to attacker and players from {SquadLife::Data::%attacker's uuid%::Soulbounds::*}

command /claimkill <player>:
    executable by: players
    trigger:
        if metadata value "Claimable" of arg-1 = true:
            broadcast "&a%player% has claimed a kill on %arg-1%"
            delete metadata value "Claimable" of arg-1
            if any: 
                {SquadLife::Roles::Boogeyman::*} contains uuid of player
                players from {SquadLife::Data::%uuid of player%::Soulbounds::*} contains any of {SquadLife::Roles::Boogeyman::*}
            then:
                CureBoogeyman(player)
                send "&a%player% has claimed a kill on %arg-1%. You are cured" to player and players from {SquadLife::Data::%player's uuid%::Soulbounds::*}


command /amitheboogeyman:
    executable by: players 
    trigger:
        if any: 
            {SquadLife::Roles::Boogeyman::*} contains uuid of player
            players from {SquadLife::Data::%uuid of player%::Soulbounds::*} contains any of {SquadLife::Roles::Boogeyman::*}
        then:
            send "&7You are &c&lThe Boogeyman&r&7. You or one of your soulbounds must kill another player before time runs out. If you &cfail &7everyone in your squad will be set down to their last life."
            play sound "block.note_block.didgeridoo" to player
        else:
            send "&7You are &a&lNOT the Boogeyman&r&7."
            play sound "block.note_block.bell" to player
        

function CureBoogeyman(p: player):
    send title "&a&lYou are cured!" with subtitle "" to {_p} and players from {SquadLife::Data::%{_p}'s uuid%::Soulbounds::*}
    play sound "block.note_block.bell" to {_p} and players from {SquadLife::Data::%{_p}'s uuid%::Soulbounds::*}
    set {_i} to first index of (any of ({_p}'s uuid and {SquadLife::Data::%{_p}'s uuid%::Soulbounds::*})) in {SquadLife::Roles::Boogeyman::*}
    delete {SquadLife::Roles::Boogeyman::%{_i}%}
    

function FailBoogeymen():
    set {_failed::*} to all players where [{SquadLife::Roles::Boogeyman::*} contains any of (uuid of input and players from {SquadLife::Data::%uuid of input%::Soulbounds::*})]
    send title "&cYou have failed!" to {_failed::*}
    loop {_failed::*}:
        set {SquadLife::Data::%uuid of loop-value%::Lives} to 1
        loop {SquadLife::Data::%uuid of loop-value%::Soulbounds::*}:
            player from loop-value-2 isn't set
            set {SquadLife::Data::%loop-value-2%::Lives} to 1
            UpdateTeam(offline player from loop-value-2)

        UpdateTeam(loop-value)
    clear {SquadLife::Roles::Boogeyman::*}
    
            